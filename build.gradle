apply plugin: 'java'

group = 'com.pi4j'
version = '0.0.1'

description = """Pi4J :: MINIMAL EXAMPLE :: Sample minimal project"""
project.buildDir = 'target'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// Copy the Java modules in the distribution directory
plugins.withType(JavaPlugin).configureEach {
    java {
        modularity.inferModulePath = true
    }
}
task copyDependencies(type: Copy) {
    from configurations.default
    into 'target/distribution'
}
build.finalizedBy copyDependencies

// Copy the generated application JAR in the distribution directory
task copyJar(type: Copy) {
    from jar
    into 'target/distribution'
}
build.finalizedBy copyJar

// Copy the run script in the distribution directory
task copyRun(type: Copy) {
    from 'assets/run.sh'
    into 'target/distribution'
}
build.finalizedBy copyRun

// Dependencies for this application, until V2 is released, snapshot is required here
repositories {
    mavenCentral()
    maven {
        name 'snapshots'
        url 'https://oss.sonatype.org/content/groups/public'
    }
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.0-alpha0'
    implementation 'org.slf4j:slf4j-simple:2.0.0-alpha0'
    implementation 'com.pi4j:pi4j-core:2.0-SNAPSHOT'
    implementation 'com.pi4j:pi4j-plugin-raspberrypi:2.0-SNAPSHOT'
    implementation 'com.pi4j:pi4j-plugin-pigpio:2.0-SNAPSHOT'
}